---
layout: post
title: "SLAE x86 Exam Part 5.1"
description: "SLAE x86 Exam Part 5.1: MSF Payload Analysis #1"
modified: 2018-06-03T13:15:00-05:00
tags: [pentest, asm, x86, slae]
---

# Overview
This blog post has been created for completing the requirements of the
SecurityTube Linux Assembly Expert certification:

[http://securitytube-training.com/online-courses/securitytube-linux-assembly-expert/](/http://securitytube-training.com/online-courses/securitytube-linux-assembly-expert/)

Student ID: SLAE-1260

---
# Payload #1

This is the first payload selected for [SLAE Part 5](/slae-5-msf).  To keep
things interesting, I generated these payloads randomly and blindly, so this
challenge became an exercise in reverse engineering.

Right off the bat, we see a jump/call/pop sequence:

{% highlight nasm %}
_start:
    jmp short 0x38 ; LBL1
LBL2:
    mov eax,0x5
    pop ebx
    ...
LBL1:
    call 0x2 ; LBL2
    das
    gs jz 0xa4
    das
    push dword 0x6e74736f
    popa
    insd
    gs
    db 0x00
{% endhighlight %}

...Well...sort of.  Instead
of the usual declaration immediately following `CALL`, we see a chain of unusual
assembly instructions followed by `db 0x00`.  If I had to guess, I'd say that's
a string declaration...

{% highlight bash %}
$ echo "2F6574632F686F73746E616D6500" | xxd -r -p; echo
/etc/hostname
{% endhighlight %}

Indeed, it matches the "/etc/hostname" string in our `generate.sh` script.

Moving forward, we see a series of 3 syscalls: `0x5` (`OPEN`), `0x3` (`READ`),
and `0x4` (`WRITE`).  Since the calls to `OPEN`/`READ` immediately follows our 
JMP/CALL/POP sequence, we can surmise that we are reading the file whose path
is currently stored in EBX. The subsequent call to `WRITE` loads `1` into EBX,
indicating that we are writing to STDOUT.  

{% highlight nasm %}
; int open(const char *pathname, int flags, mode_t mode);
mov eax,0x5     ; OPEN syscall
pop ebx         ; Load address of string
xor ecx,ecx     ; Clear ECX
int 0x80        ; Exec interrupt

; ssize_t read(int fd, void *buf, size_t count);
mov ebx,eax     ; Save returned FD
mov eax,0x3     ; READ syscall
mov edi,esp     ; Read into address contained in EDI (*buf)
mov ecx,edi     ; Load (*buf) argument -> ECX
mov edx,0x1000  ; Read up to 4,096 bytes
int 0x80        ; Exec interrupt

; ssize_t write(int fd, const void *buf, size_t count);
mov edx,eax     ; Save number of returned bytes -> EDX
mov eax,0x4     ; WRITE syscall
mov ebx,0x1     ; Write to STDOUT
int 0x80        ; Exec interrupt
{% endhighlight %}

Finally, we exit with return code 0:

{% highlight nasm %}
mov eax, 0x1    ; EXIT syscall
mov ebx, 0x0    ; Return code 0
int 0x80        ; Exec interrupt
{% endhighlight %}

Putting it all together, it looks like we're reading a file and printing it to
STDOUT.  That looks a lot like the `read_file` module to me.  

{% highlight bash %}
$ msfvenom -p linux/x86/read_file PATH=/etc/hostname | ndisasm -b32 - 
No platform was selected, choosing Msf::Module::Platform::Linux from the payload
No Arch selected, selecting Arch: x86 from the payload
No encoder or badchars specified, outputting raw payload
Payload size: 75 bytes

00000000  EB36              jmp short 0x38
00000002  B805000000        mov eax,0x5
00000007  5B                pop ebx
00000008  31C9              xor ecx,ecx
0000000A  CD80              int 0x80
0000000C  89C3              mov ebx,eax
0000000E  B803000000        mov eax,0x3
00000013  89E7              mov edi,esp
00000015  89F9              mov ecx,edi
00000017  BA00100000        mov edx,0x1000
0000001C  CD80              int 0x80
0000001E  89C2              mov edx,eax
00000020  B804000000        mov eax,0x4
00000025  BB01000000        mov ebx,0x1
0000002A  CD80              int 0x80
0000002C  B801000000        mov eax,0x1
00000031  BB00000000        mov ebx,0x0
00000036  CD80              int 0x80
00000038  E8C5FFFFFF        call 0x2
0000003D  2F                das
0000003E  657463            gs jz 0xa4
00000041  2F                das
00000042  686F73746E        push dword 0x6e74736f
00000047  61                popa
00000048  6D                insd
00000049  65                gs
0000004A  00                db 0x00
{% endhighlight %}

There it is, right down to the wonky string at the end.  

## Screenshots

![Execution of Payload #1]({{ "/assets/images/5.1-msf1.png" | absolute_url}})

On to [Payload #2](/slae-5-msf2)!
